/**********************************************************************************
 * Copyright (c) 2025 SandAI. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *********************************************************************************/

/**********************************************************************************
 * Copyright (c) 2025 DeepSeek. All Rights Reserved.
 *
 * Licensed under the MIT License.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 *********************************************************************************/

#pragma once

#include "configs.cuh"
#include "exception.cuh"
#include "utils.cuh"

namespace magi_attn_comm::grpcoll {

// TODO: support non-inplace updated template parameter
template <typename dtype_t, size_t kAlignment = 1>
struct Buffer {
 private:
  uint8_t* ptr;

 public:
  int total_bytes;

  DEVICE_INLINE Buffer() : ptr(nullptr), total_bytes(0) {}

  DEVICE_INLINE Buffer(void*& gbl_ptr, int num_elems, int elem_offset = 0) {
    total_bytes = num_elems * sizeof(dtype_t); // the total bytes of this block
    ptr = reinterpret_cast<uint8_t*>(gbl_ptr) + elem_offset * sizeof(dtype_t); // the start ptr within this block

    // advance `ptr` to align the address to `kAlignment` bytes if necessary
    size_t pad_bytes_to_align = 0;
    if constexpr (kAlignment > 1) {
      auto ptr_addr = reinterpret_cast<size_t>(ptr);
      pad_bytes_to_align = ptr_addr % kAlignment;
      if (pad_bytes_to_align != 0)
        pad_bytes_to_align = kAlignment - pad_bytes_to_align;
      ptr += pad_bytes_to_align;
    }

    // In-place advance the `gbl_ptr` across this block
    // and further advance the pad bytes as the `ptr` if necessary
    gbl_ptr = reinterpret_cast<uint8_t*>(gbl_ptr) + total_bytes + pad_bytes_to_align;
  }

  DEVICE_INLINE Buffer advance_also(void*& gbl_ptr) {
    gbl_ptr = reinterpret_cast<uint8_t*>(gbl_ptr) + total_bytes;
    return *this;
  }

  DEVICE_INLINE dtype_t* buffer() {
    return reinterpret_cast<dtype_t*>(ptr);
  }

  DEVICE_INLINE dtype_t& operator[](int idx) {
    return buffer()[idx];
  }
};

template <typename dtype_t, int kNumRanks = 1>
struct AsymBuffer {
 private:
  uint8_t* ptrs[kNumRanks];
  int num_bytes;

 public:
  int total_bytes;

  DEVICE_INLINE AsymBuffer(void*& gbl_ptr, int num_elems, int num_ranks, int sm_id = 0, int num_sms = 1, int offset = 0) {
    GRPCOLL_STATIC_ASSERT(kNumRanks == 1, "");
    num_bytes = num_elems * sizeof(dtype_t);

    int per_channel_bytes = num_bytes * num_ranks;
    total_bytes = per_channel_bytes * num_sms;
    ptrs[0] = reinterpret_cast<uint8_t*>(gbl_ptr) + per_channel_bytes * sm_id + num_bytes * offset;
    gbl_ptr = reinterpret_cast<uint8_t*>(gbl_ptr) + total_bytes;
  }

  DEVICE_INLINE AsymBuffer(void** gbl_ptrs, int num_elems, int num_ranks, int sm_id = 0, int num_sms = 1, int offset = 0) {
    GRPCOLL_STATIC_ASSERT(kNumRanks > 1, "");
    num_bytes = num_elems * sizeof(dtype_t);

    int per_channel_bytes = num_bytes * num_ranks;
    total_bytes = per_channel_bytes * num_sms;
    for (int i = 0; i < kNumRanks; ++i) {
      ptrs[i] = reinterpret_cast<uint8_t*>(gbl_ptrs[i]) + per_channel_bytes * sm_id + num_bytes * offset;
      gbl_ptrs[i] = reinterpret_cast<uint8_t*>(gbl_ptrs[i]) + total_bytes;
    }
  }

  DEVICE_INLINE void advance(int shift) {
#pragma unroll
    for (int i = 0; i < kNumRanks; ++i)
      ptrs[i] = ptrs[i] + shift * sizeof(dtype_t);
  }

  DEVICE_INLINE AsymBuffer advance_also(void*& gbl_ptr) {
    gbl_ptr = reinterpret_cast<uint8_t*>(gbl_ptr) + total_bytes;
    return *this;
  }

  template <int kNumAlsoRanks>
  DEVICE_INLINE AsymBuffer advance_also(void** gbl_ptrs) {
    for (int i = 0; i < kNumAlsoRanks; ++i)
      gbl_ptrs[i] = reinterpret_cast<uint8_t*>(gbl_ptrs[i]) + total_bytes;
    return *this;
  }

  DEVICE_INLINE dtype_t* buffer(int idx = 0) {
    GRPCOLL_STATIC_ASSERT(kNumRanks == 1, "`buffer` is only available for single rank case");
    return reinterpret_cast<dtype_t*>(ptrs[0] + num_bytes * idx);
  }

  DEVICE_INLINE dtype_t* buffer_by(int rank_idx, int idx = 0) {
    GRPCOLL_STATIC_ASSERT(kNumRanks > 1, "`buffer` is only available for single rank case");
    return reinterpret_cast<dtype_t*>(ptrs[rank_idx] + num_bytes * idx);
  }
};

template <typename dtype_t, bool kDecoupled = true>
struct SymBuffer {
 private:
  // NOTES: for non-decoupled case, `recv_ptr` is not used
  uint8_t* send_ptr;
  uint8_t* recv_ptr;
  int num_bytes;

 public:
  int total_bytes;

  DEVICE_INLINE SymBuffer(void*& gbl_ptr, int num_elems, int num_ranks, int sm_id = 0, int num_sms = 1) {
    num_bytes = num_elems * sizeof(dtype_t);

    int per_channel_bytes = num_bytes * num_ranks;
    total_bytes = per_channel_bytes * num_sms * (static_cast<int>(kDecoupled) + 1);
    send_ptr = reinterpret_cast<uint8_t*>(gbl_ptr) + per_channel_bytes * sm_id;
    recv_ptr = reinterpret_cast<uint8_t*>(gbl_ptr) + per_channel_bytes * (sm_id + num_sms);
    gbl_ptr = reinterpret_cast<uint8_t*>(gbl_ptr) + total_bytes;
  }

  DEVICE_INLINE dtype_t* send_buffer(int idx = 0) {
    GRPCOLL_STATIC_ASSERT(kDecoupled, "`send_buffer` is only available for non-decoupled case");
    return reinterpret_cast<dtype_t*>(send_ptr + num_bytes * idx);
  }

  DEVICE_INLINE dtype_t* recv_buffer(int idx = 0) {
    GRPCOLL_STATIC_ASSERT(kDecoupled, "`recv_buffer` is only available for non-decoupled case");
    return reinterpret_cast<dtype_t*>(recv_ptr + num_bytes * idx);
  }

  DEVICE_INLINE dtype_t* buffer(int idx = 0) {
    GRPCOLL_STATIC_ASSERT(not kDecoupled, "`buffer` is only available for decoupled case");
    return reinterpret_cast<dtype_t*>(send_ptr + num_bytes * idx);
  }
};

} // namespace magi_attn_comm::grpcoll
